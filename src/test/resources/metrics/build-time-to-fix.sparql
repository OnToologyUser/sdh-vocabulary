#
# #-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=#
#   This file is part of the Smart Developer Hub Project:
#     http://www.smartdeveloperhub.org/
#
#   Center for Open Middleware
#     http://www.centeropenmiddleware.com/
# #-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=#
#   Copyright (C) 2015 Center for Open Middleware.
# #-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=#
#   Licensed under the Apache License, Version 2.0 (the "License");
#   you may not use this file except in compliance with the License.
#   You may obtain a copy of the License at
#
#             http://www.apache.org/licenses/LICENSE-2.0
#
#   Unless required by applicable law or agreed to in writing, software
#   distributed under the License is distributed on an "AS IS" BASIS,
#   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
#   See the License for the specific language governing permissions and
#   limitations under the License.
# #-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=#
#   Artifact    : org.smartdeveloperhub.vocabulary:sdh-vocabulary:1.0.0-SNAPSHOT
#   Bundle      : sdh-vocabulary-1.0.0-SNAPSHOT.jar
# #-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=#
#

PREFIX oslc_auto: <http://open-services.net/ns/auto#>
PREFIX ci: <${vocabulary.base}ci#>

SELECT ?build ?broken ?break ?fixed ?fix ((?fix - ?break) as ?duration)
WHERE {
  ?fixed oslc_auto:executesAutomationPlan ?build ;
         ci:finished ?fix .
  ?broken oslc_auto:executesAutomationPlan ?build ;
          ci:finished ?break .
  {
    SELECT ?build ?fix (MIN(?brokenTime) as ?break)
    WHERE {
      ?broken oslc_auto:executesAutomationPlan ?build ;
              ci:finished ?brokenTime .
      {
        SELECT ?build ?broken (MIN(?fixTime) as ?fix)
        WHERE {
          ?fixed oslc_auto:executesAutomationPlan ?build ;
                 ci:finished ?fixTime ;
                 ci:hasResult ?r1 .
          ?r1 oslc_auto:verdict oslc_auto:passed .
          ?broken oslc_auto:executesAutomationPlan ?build ;
                  ci:finished ?breakTime ;
                  ci:hasResult ?r2 .
          ?r2 oslc_auto:verdict oslc_auto:failed .
          FILTER(?breakTime < ?fixTime)
        }
        GROUP BY ?build ?broken
      }
    }
    GROUP BY ?build ?fix
  }
}
ORDER BY ?build ?break
